# Firstly, we need to define what tags should be considered sentence delimiters. For this example, only full stop is set as delimiter.
DELIMITERS = "<.>" "<!>" "<?>" ;
SOFT-DELIMITERS = "<,>" ;

SUBREADINGS = LTR ;

LIST Det = det ; 
LIST N = n ;
LIST A = adj ;
LIST Pron = prn ;
LIST Adv = adv ; 
LIST CC = cnjcoo ;
LIST Rel = rel ; 
LIST CS = cnjsub ;
LIST Prop = np ;
LIST Verb = vblex vbhaver vbmod vbser ;


LIST Pr = pr ; 

LIST Prep/di = "<di>"i "<de>"i ;
LIST Prep/a = "<a>"i "<davanti a>"i ;
LIST Prep/pi = "<pi>"i ;
LIST Prep/cu = "<cu>"i ;
LIST Prep/nta = "<nta>"i "<nti>"i ;

LIST Ind = ind ; 
LIST Def = def ;

LIST CC/e = "<e>" ;
LIST CC/o = "<o>" ;
LIST CS/ca = "<ca>" ;

LIST Haver = vbhaver ;
LIST Ser = vbser ;


LIST Chi = "<chi>" ;
LIST Cristianu = "cristianu" ;

LIST Imper = imp ;
LIST PrfPrc = pp ;

LIST Sg3 = (p3 sg) ;
LIST ProRef = (prn pro ref) ;

LIST Cog = cog ;
LIST Top = top ;
LIST Al = al ;
LIST Ant = ant ; 

LIST Sg = sg ;
LIST Pl = pl ; 
LIST Sp = sp ;

LIST Adj/pl = (adj pl) ;


LIST Ifi = ifi ; 
LIST Pri = pri ;

LIST Msc = m ;
LIST Fem = f ; 
LIST MscFem = mf ;

LIST Excl = "!" ;
LIST Cm = cm ;

LIST unknown = ("\\*.*"r) ; 

SET NotMsc = Fem | MscFem ;
SET NotFem = Msc | MscFem ;

SET NotSg = Pl | Sp ;
SET NotPl = Sg | Sp ;

SET Genero = NotMsc | NotFem ;
SET Numero = NotSg | NotPl ;

SET Fin = Ifi | Pri ; 




SECTION


REMOVE SUB:1 Det IF (0 Prep/di OR Prep/a OR Prep/pi OR Prep/cu OR Prep/nta) ;
 # "di" y "a" etc. deberían quedar como preposiciones simples por defecto.
 # necesitamos esta regla porque a veces se escribe siciliano sin tildes, 
 # y allí "a" y "â" son ambiguos

SELECT CC IF (0 CC/e OR CC/o) ;

SELECT CS IF (0 CS/ca) ;

SELECT A + $$Genero IF (-1C N + $$Genero) ;

SELECT Haver IF (1C PrfPrc) ;

SELECT Sg3 IF (-1 ProRef) ;
 # Nun si sapi quannu ci fu l’affirmazzioni di lu Cristianèsimu a Catania.

SELECT Fin IF (-1C Prop) (0 Fin OR N) ;

SELECT Sg3 IF (-1C Prop) ;

SELECT CS IF (1 Verb) ;

SELECT Rel IF (0 Chi) (1 Fin) ;

REMOVE Imper IF (NOT 1* Excl) (NOT 1 Cm) ;

SELECT Adv IF (-1C Pron) (0 Adv OR Det) (1 Fin) ;

REMOVE Ind IF (0 Ind OR Pr) (1 Def) ;

SELECT N IF (-1 Det OR Pr) (0 Cristianu) ;



SELECT CS IF (1 Verb) ;

SELECT Det IF (-1C Ser) (0 Pr) ;

SELECT Msc IF (0 Adj/pl) ;




AFTER-SECTIONS

REMOVE SUB:1 Det (NOT 0 Imper) ;

# Resources:
# http://visl.sdu.dk/cg3.html
# http://groups.google.com/group/constraint-grammar
# http://kevindonnelly.org.uk/2010/05/constraint-grammar-tutorial/
# http://wiki.apertium.org/wiki/Constraint_Grammar
# http://wiki.apertium.org/wiki/Apertium_and_Constraint_Grammar

